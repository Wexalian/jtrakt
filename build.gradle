buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 11


group 'com.wexalian.jtrakt'
version = externalVersion

repositories {
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'

    compile 'com.google.code.findbugs:jsr305:3.0.2'
    compile 'com.google.guava:guava:26.0-jre'
//    compile 'org.apache.commons:commons-lang3:3.8.1'
//    compile 'org.apache.commons:commons-io:1.3.2'
}
compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'jtrakt'
                packaging = 'jar'
                description = 'A Java wrapper and utility library for the trakt.tv API'
                url = 'http://www.wexalian.com/jtrakt'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Wexalian'
                        name = 'Lars Gerrits'
                        email = 'larsgerrits99@gmail.com'
                        organization = 'wexalian'
                        organizationUrl = 'http://wexalian.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Wexalian/jtrakt.git'
                    developerConnection = 'scm:git:ssh://github.com/Wexalian/jtrakt.git'
                    url = 'http://www.github.com/Wexalian/jtrakt'
                }
            }
        }
    }
    repositories {
        maven {
            name 'deploy'
            url = version.endsWith('SNAPSHOT') ? deployRepoUrlSnapshot : deployRepoUrl
            credentials {
                username = deployRepoUsername
                password = deployRepoPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}